include("engrave.inc.gcmc");	
 vl = { };
function DrawTick(xs,ys,hs)
{
    vl += {[-,-,1]};
    vl += {[xs,ys,-]};
    vl += {[-,-,-1]};
    vl += {[-,ys+hs,-]};    
}

function GetMaxX( vecl )
{
    local xmax = 0;
    foreach(vecl; v) 
    {
		if( !isundef(v.x)  && v.x > xmax)
        {
			xmax = v.x;
		}
	}
	return xmax;    
}

function DrawLegend(xs,ys, step, hs, text)
{
        /* Typeset the text */
        local vlt = { };
        local sf = 4;
        
        DrawTick(xs,ys,hs);
        DrawTick(xs+step,ys,hs);
        local lbltext;
        if ( isfloat( text ) )        {    lbltext = float_to_string(text,2);  }
        else                            {    lbltext = to_string(text) ;        }
        
        vlt = typeset(lbltext , FONT_HSANS_1);
        
        /* Scale XY to real world coordinates */
        /* Do not touch the Z-coordinate, the engrave() function needs the original */
        vlt = scale(vlt, [sf, sf]);

        /* Place somewhere in XY space */
        /* Again without touching the Z-coordinate */
        vlt += [xs + step + 3, ys ];
        vl += vlt;
}


function DrawLabel(xs,ys, text)
{
        /* Typeset the text */
        local vlt = { };
        local sf = 5;
        vlt = typeset(to_string(text), FONT_HSANS_1);

        /* Scale XY to real world coordinates */
        /* Do not touch the Z-coordinate, the engrave() function needs the original */
        vlt = scale(vlt, [sf, sf]);

        /* Place somewhere in XY space */
        /* Again without touching the Z-coordinate */
        vlt += [xs - GetMaxX(vlt)/2, ys-8];
      //  vlt += [xs , ys];
        vl += vlt;
}

function MakeRule( StartX, StartY, Len, Step, n1, n2, h )
{
    local x = StartX;
    local EndX= StartX + Len;
    local y = StartY;
    local h1 = h/4;
    local h2 = h/2;
    local valtxt = 0;
    while(  x <= EndX )
    {
        DrawTick( x,y,h);
        DrawLabel(x,y, to_none(valtxt) );
        valtxt++;
        x+=Step;
        for( i2 = 0; i2 < n2 && x < EndX; i2++ )
        {
            for( i1 = 0; i1 < n1 && x < EndX; i1++ )
            {
                DrawTick(x,y,h1);
                x+=Step;
            }
            if ( x <= EndX  && i2 < (n2-1) )  // do not draw last
            { 
                DrawTick(x,y,h2);
                x+=Step;
            }
        }
    }
    
    DrawLegend(StartX,StartY + h + 5, Step, h1, Step);
    engrave(vl, 5mm, 1mm);
}

function DrawTick1(pos1,pos2)
{
    vl += {[-,-,1]};
    vl += {pos1};
    vl += {[-,-,-1]};
    vl += {pos2};    
}

function DrawLabel1(xCenter, yCenter,Radius,angle, text,hh)
{
        /* Typeset the text */
        local vlt = { };
        local sf = 5;
        vlt = typeset(to_string(text), FONT_HSANS_1);
        to_mm(vlt);
        /* Scale XY to real world coordinates */
        /* Do not touch the Z-coordinate, the engrave() function needs the original */
        vlt = scale(vlt, [sf, sf]);
        
        

        /* Place somewhere in XY space */
        /* Again without touching the Z-coordinate */
        hh = to_none(hh);
        if (   hh < 0 )
            { hh = 0; }
        
        local dc = [ GetMaxX(vlt)/2, hh+8];
        vlt -= dc;
        
        local posl = [xCenter, yCenter,-] + rotate_xy([0mm,Radius,-], angle );
        
        vlt = rotate_xy(vlt, angle-180deg);
        vlt += posl;
        vl += vlt;
}

function MakeRoundRule( xCenter, yCenter, Radius, AngleBase, AngleStart, AngleEnd, Step, n, h )
{
    local a = AngleBase;
    local h1 = h/2;
    local valtxt = AngleStart;
    local AngleBaseEnd = AngleBase + AngleEnd - AngleStart;
    vl = {};
    while(  a <= AngleBaseEnd )
    {
        
        v1 = rotate_xy([0mm,h,-], a );
        pos = [xCenter, yCenter,-] + rotate_xy([0mm,Radius,-], a );
        DrawTick1(pos, pos + v1 ); 
        valtxt = a - AngleBase + AngleStart;
        DrawLabel1(xCenter, yCenter,Radius,a,to_none(valtxt),h);
        a+=Step;        
        for( i = 0; i < n && a < AngleBaseEnd; i++ )
        {
            pos = [xCenter, yCenter,-] + rotate_xy([0mm,Radius,-], a );
            v1 = rotate_xy([0mm,h1,-], a );
            DrawTick1(pos, pos + v1 ); 
            a += Step;
        } 
    }
    engrave(vl, 5mm, 1mm);
}


feedrate(200);
MakeRoundRule( 0mm, 0mm, 100mm, 90deg, -90deg, 90deg, 1deg, 9, 10mm );
MakeRoundRule( 0mm, 0mm, 150mm, 60deg, -30deg, 30deg, 2deg, 4, 10mm );
MakeRoundRule( 0mm, 0mm, 150mm, 180deg, 0deg, 90deg, 1deg, 9, -10mm );
MakeRule(-50mm,0mm, 100mm,1mm,4, 2,10mm);
MakeRule(-50mm,50mm,5in,0.25in,3, 1,0.5in);

